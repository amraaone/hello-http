plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.google.protobuf' version '0.9.4'
	id 'maven-publish'
	id 'io.github.lognet.grpc-spring-boot' version '5.0.0'
}

group = 'com.example.hellohttp'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

bootJar {
	mainClass = 'com.example.hellohttp.hellohttp.HelloHttpApplication'
	archiveFileName = 'hello-http.jar'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://maven.pkg.github.com/amraaone/hello-grpc"
		credentials {
			username = System.getenv("GPR_USERNAME")
			password = System.getenv("GPR_PASSWORD")
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// dapr
	implementation 'io.dapr:dapr-sdk-springboot:1.10.0'

	// grpc
	implementation 'com.google.protobuf:protobuf-java:3.25.1'
	implementation 'org.example.hellogrpc.protobuf:hello_protobuf:0.0.1'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.25.1'
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc{}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
